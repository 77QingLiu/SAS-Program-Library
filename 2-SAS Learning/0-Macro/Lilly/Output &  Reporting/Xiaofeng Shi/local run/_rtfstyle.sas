/*soh*************************************************************************
Eli Lilly and Company - Global Statistical Sciences
CODE NAME                 : _RTFSTYLE.SAS
CODE TYPE                 : Limited-use Module
PROJECT NAME (optional)   : Standard output macro for Lilly China internal use 

DESCRIPTION               : Creates an TEMPORYRY RTF TEMPLATE.                            

SOFTWARE/VERSION#         : SAS/Version 9.2
INFRASTRUCTURE            : SDD version 3.4

LIMITED-USE MODULES       : n/a

BROAD-USE MODULES         : n/a
INPUT                     : n/a
OUTPUT                    : n/a
 
VALIDATION LEVEL          : 3
REQUIREMENTS              : n/a
ASSUMPTIONS               : 
--------------------------------------------------------------------------------
--------------------------------------------------------------------------------
BROAD-USE MODULE/LIMITED-USE MODULE SPECIFIC INFORMATION: n/a

BROAD-USE MODULE TEMPORARY OBJECT PREFIX: n/a

PARAMETERS:

Name          Type         Default            Description and Valid Values
---------     ------------ ------------------ ----------------------------------

USAGE NOTES:
   Users may call the _RTFSTYLE macro to set up the RTF TEMPLATE. But this macro
   could be only used for those RTF outputs which are generated by ODS RTF. This
   macro is called by macro output_pre automatically. Users don't need to invoke
   it by themselves.

TYPICAL WAYS TO EXECUTE THIS CODE AND DESCRIPTION, if applicable:

%_rtfstyle;

--------------------------------------------------------------------------------
--------------------------------------------------------------------------------
REVISION HISTORY SECTION:

       Author &
Ver# Peer Reviewer        Code History Description
---- ----------------     ------------------------------------------------
1.0   Xiaofeng Shi        Original version of the code
      Weishan Shi
**eoh************************************************************************/

%macro _rtfstyle ;

%PUT NOTE: (&prp_macname: &sysmacroname) ----------------------;
%PUT NOTE: (&prp_macname: &sysmacroname) &SYSMACRONAME starts.;
%PUT NOTE: (&prp_macname: &sysmacroname) ----------------------;

%*****************************************************************************************************************;
%**     Define a basic table Style That has elements within it that could be used by all intext tables          **;
%*****************************************************************************************************************;

ods path rtf_temp(update)
         SASUSER.templat(update)
         SASHELP.tmplmst(read);

proc template;

   define style rtf_temp/store=rtf_temp;
   parent =styles.rtf;

   %** important thing to notice is the protectspecialchars, which allows us to push the RTF codes through **;
   style table from table / rules=group frame=hsides cellspacing=0 cellpadding=0
                            outputwidth =_undef_ protectspecialchars=off;
						
   %** these are the margins for A4 portrait.  Other margin sizes can be set using different styles **;
   replace body from document /

   %if %upcase(&csv_orient)=PORTRAIT %then %do;
      bottommargin = 1in
      topmargin    = 0.5in
      rightmargin  = 1in
      leftmargin   = 1in;
   %end;
   %else  %if %upcase(&csv_orient)=LANDSCAPE %then %do;
      bottommargin = 1in
      topmargin    = 0.5in
      rightmargin  = 1in
      leftmargin   = 1in;
   %end;

   %** these fonts are so styled to work for SDD and Windows **;
   replace fonts /
      'CellFont'             = ("Courier New",8pt)
      'TitleFont2'           = ("Courier New",8pt)
      'TitleFont'            = ("Courier New",8pt)
      'StrongFont'           = ("Courier New",8pt)
      'EmphasisFont'         = ("Courier New",8pt)
      'FixedEmphasisFont'    = ("Courier New",8pt)
      'FixedStrongFont'      = ("Courier New",8pt)
      'FixedHeadingFont'     = ("Courier New",8pt)
      'BatchFixedFont'       = ("Courier New",8pt)
      'FixedFont'            = ("Courier New",8pt)
      'headingEmphasisFont'  = ("Courier New",8pt)
      'headingFont'          = ("Courier New",8pt)
      'docFont'              = ("Courier New",8pt)
      'footFont'              = ("Courier New", 8 pt);

   %** the following set the header and cell styles which we can use in a generic fashion later on **;
   replace headersAndFooters from cell /
      font = fonts('HeadingFont')
      foreground = colors('headerfg')
      background = colors('contentbg');

   replace data from cell /
      background = colors('contentbg');

   style line from cell /    
      font = fonts('CellFont')
      cellspacing = 0
      cellwidth   = 40cm
      cellpadding = 0
      background  = black
      cellheight  = 5;
   style left from cell /   
      font = fonts('CellFont')
      just = l
      background = _UNDEF_;
   style right from cell / 
      font = fonts('CellFont')
      just = r
      background = _UNDEF_;
   style rightbr from cell /
      font = fonts('CellFont')
      just = r
      pretext = '('
      posttext = ')'
      background = _UNDEF_;
   style centre from cell /  
      font = fonts('CellFont')
      just = c
      background = _UNDEF_;
   style pageno /
      foreground = white
      font_size = 0.1pt;
   style SystemFooter from SystemFooter                                
      "Controls system footer text." /                                     
      just = l ;  
   end;
quit;

%mend _rtfstyle;

%***********************************************************************;
%***********    END OF _RTFSTYLE MACRO   ******************************;
%***********************************************************************;
