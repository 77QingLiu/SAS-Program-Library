 *************************************************************;
 *** %DEF_OS MACRO DEFINITION                              ***;
 ***                                                       ***;
 *** FUNCTION:                                             ***;
 ***    Resolve operating platform and set-up global macro ***;
 ***    variables for use later in setup.sas and in other  ***;
 ***    programs throughout study                          ***;
 ***                                                       ***;
 *** MACRO ARGUMENTS:                                      ***;
 ***    project - Project/study name                       ***;
 ***    client  - Client name                              ***;
 ***    tims    - Tims code                                ***;
 ***                                                       ***;
 *** GLOBAL MACRO VARIABLES CREATED:                       ***;
 ***    _project - As project macro argument               ***;
 ***    _client  - As client macro argument                ***;
 ***    _tims    - As tims macro argument                  ***;
 ***    _projpre - Path to top level project directory -   ***;
 ***               See definition of Project/ in B&P       ***;
 ***               Filing Guidelines (WW-004-02).          ***;
 ***               Do not include suffix in path.          ***;
 ***    _divider - Character that divides directory levels ***;
 ***    _suffix  - Last character in path name             ***;
 *************************************************************;
 %macro def_os (project=, client=, tims=);

         %** SETUP GLOBAL MACRO VARIABLES ***;
         %global _projpre _divider _suffix _project _client _tims;
         %let _project=&project;
         %let _client=&client;
         %let _tims=&tims;

         %** RESOLVE OPERATING PLATFORM AND SET MACRO VARIABLES ***;
         %** WINDOWS PLATFORM ***;
         %if &sysscp=WIN %then %do;
                 %** _PROJPRE: EXAMPLE J:\BDM\&CLIENT.\&TIMS.\STATS ***;
                 %let _projpre=;
                 %let _divider=\;
                 %let _suffix=\;
         %end;
         %** VMS PLATFORM ***;
         %else %if &sysscp=VMS_AXP %then %do;
                 %** _PROJPRE U.K. EXAMPLE &PROJECT._DISK:[&PROJECT..&PROJECT..STATS ***;
                 %** _PROJPRE U.S. EXAMPLE DISK$&PROJECT.:[&PROJECT..STATS ***;
                 %let _projpre=;
                 %let _divider=.;
                 %let _suffix=];
         %end;
         %** UNIX PLATFORM ***;
         %else %if &sysscp=HP IPF %then %do;
                 %** _PROJPRE: SET TO EQUIVALENT FOLDER IN UNIX AS WINDOWS/VMS ***;
                 %** _PREJPRE: EXAMPLE /projects/&project./stats *** (default project structure) ***;
                 %let _projpre=/projects/&project./stats;
                 %let _divider=/;
                 %let _suffix=/;
         %end;

         %** REPORT CONTENTS OF MACRO VARIABLES TO LOG ***;
         %put;
         %put NOTE: Running on &sysscp platform.;
         %put NOTE: def_os macro has set-up the following global macro variables:-;
         %put;
         %put NOTE: Client (_CLIENT)                               : &_client;
         %put NOTE: TIMS Code (_TIMS)                              : &_tims;
         %put NOTE: Project (_PROJECT)                             : &_project;
         %put NOTE: Path to top level project directory (_PROJPRE) : &_projpre;
         %put NOTE: Path name divider (_DIVIDER)                   : &_divider;
         %put NOTE: Path name suffix (_SUFFIX)                     : &_suffix;
         %put;

 %mend def_os;
 