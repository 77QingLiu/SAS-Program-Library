%macro searep (rootdir=, sub=, from=, to=, test=);

   %******************************************************************************;
   %*                          PAREXEL INTERNATIONAL LTD                          ;
   %*                                                                             ;
   %* CLIENT:            PAREXEL                                                  ;
   %*                                                                             ;
   %* PROJECT:           Search and Replace utility macro                         ;
   %*                                                                             ;
   %* TIMS CODE:         56981                                                    ;
   %*                                                                             ;
   %* SOPS FOLLOWED:     1213                                                     ;
   %*                                                                             ;
   %******************************************************************************;
   %*                                                                             ;
   %* PROGRAM NAME:      searep.sas                                               ;
   %*                                                                             ;
   %* PROGRAM LOCATION:  /opt/pxlcommon/stats/macros/sas/code/searep/ver001       ;
   %*                                                                             ;
   %******************************************************************************;
   %*                                                                             ;
   %* USER REQUIREMENTS: Locate SAS programs in current directory and sub-        ;
   %*                    directories and change text as specified.                ;
   %*                                                                             ;
   %*                    Produce message if text is found in SAS program          ;
   %*                    in different case to specified in macro argument.        ;
   %*                                                                             ;
   %*                    Contain functionality to 'test run' to check potential   ;
   %*                    changes without updating programs.                       ;
   %*                                                                             ;
   %* TECHNICAL          Refer to comments within program code.                   ;
   %* SPECIFICATIONS:                                                             ;
   %*                                                                             ;
   %* INPUT:             Macro Parameters:                                        ;
   %*                    rootdir sub from to test                                 ;
   %*                    rootdir - The root unix directory to search for SAS      ;
   %*                              programs in.                                   ;
   %*                                                                             ;
   %*                    sub     - Include sub-directories in search and replace  ;
   %*                              (Y/N)                                          ;
   %*                                                                             ;
   %*                    from    - Text string to search for (case sensitive)     ;
   %*                                                                             ;
   %*                    to      - Text string to replace all occurences of       ;
   %*                              search string with.                            ;
   %*                                                                             ;
   %*                    test    - Test mode: Enter Y to check what text          ;
   %*                              replacements would be made without updating    ;
   %*                              programs.                                      ;
   %*                                                                             ;
   %* OUTPUT:            Any text replacements made will be reported to sas log.  ;
   %*                                                                             ;
   %* PROGRAMS CALLED:   N/A                                                      ;
   %*                                                                             ;
   %* ASSUMPTIONS/       Code is Unix specific.                                   ;
   %* REFERENCES:                                                                 ;
   %*                                                                             ;
   %******************************************************************************;
   %*                                                                             ;
   %* MODIFICATION HISTORY                                                        ;
   %*-----------------------------------------------------------------------------;
   %* VERSION:           1                                                        ;
   %* AUTHOR:            Dan Higgins [Sheffield/Biostats and Programming]         ;
   %*                                                                             ;
   %* RISK ASSESSMENT                                                             ;
   %* Business:          High   [ ]: System has direct impact on the provision of ;
   %*                                business critical services either globally   ;
   %*                                or at a regional level.                      ;
   %*                    Medium [ ]: System has direct impact on the provision of ;
   %*                                business critical services at a local level  ;
   %*                                only.                                        ;
   %*                    Low    [X]: System used to indirectly support the        ;
   %*                                provision of a business critical service or  ;
   %*                                operation at a global, regional or local     ;
   %*                                level.                                       ;
   %*                    None   [ ]: System has no impact on the provision of a   ;
   %*                                business critical service or operation.      ;
   %*                                                                             ;
   %* Regulatory:        High   [ ]: System has a direct impact on GxP data and/  ;
   %*                                or directly supports a GxP process.          ;
   %*                    Medium [ ]: System has an indirect impact on GxP data    ;
   %*                                and supports a GxP process.                  ;
   %*                    Low    [X]: System has an indirect impact on GxP data or ;
   %*                                supports a GxP process.                      ;
   %*                    None   [ ]: System is not involved directly or           ;
   %*                                indirectly with GxP data or a GxP process.   ;
   %*                                                                             ;
   %* TESTING            Peer code review and review of the test output           ;
   %* METHODOLOGY:                                                                ;
   %*                                                                             ;
   %* DEVELOPER:         Dan Higgins                       Date : 22/09/2005      ;
   %*                                                                             ;
   %* SIGNATURE:         ................................  Date : ............... ;
   %*                                                                             ;
   %* CODE REVIEWER:                                       Date :                 ;
   %*                                                                             ;
   %* SIGNATURE:         ................................  Date : ............... ;
   %*                                                                             ;
   %* USER:                                                Date :                 ;
   %*                                                                             ;
   %* SIGNATURE:         ................................  Date : ............... ;
   %*                                                                             ;
   %******************************************************************************;

   %** SAVE OPTIONS ***;
   %let _source    = %sysfunc(getoption(source));
   %let _notes     = %sysfunc(getoption(notes));

   %** TURN OFF SOURCE AND NOTES OPTIONS ***;
   options nosource nonotes;

   *** SET MACRO VARIABLE TO RECURSE DIRECTORIES ***;

   %if &sub=Y %then %let rec=-R;
   %else %let rec=;

   *** CHECK FOR SAS PROGRAMS - DIRECT TO TEXT FILE ***;
   data _null_;
      call system ("ls -l &rec &rootdir > /home/users/&sysuserid./temp.txt");
   run;

   *** READ IN TEXT FILE CREATED BY UNIX ***;
   data _progs (keep=prog);
      retain dir;
      infile "/home/users/&sysuserid./temp.txt" pad missover lrecl=200;
      length dir prog line $300;
      input line 1-200;
      if _n_=1 then dir="&rootdir";
      if line=:'/' then do;
         dir=tranwrd(line,'./',"&rootdir./");
         dir=substr(dir,1,length(dir)-1);
      end;
      if index(line,'.sas ') or index(line,'.SAS') then do;
         do i=length(line) to 1 by -1;
            if substr(line,i,1)=' ' then leave;
            else prog=trim(left(dir))||'/'||substr(line,i);
         end;
         output;
      end;
   run;

   *** DELETE TEXT FILE ***;
   data _null_;
      call system ("rm /home/users/&sysuserid./temp.txt");
   run;

   *** CREATE MACRO VARIABLES FOR PROGRAMS ***;
   data _null_;
      set _progs;
      call symput ('prog'||trim(left(put(_n_,best.))),trim(left(prog)));
      call symput ('progs',trim(left(put(_n_,best.))));
   run;

   *** MAKES CHANGES TO PROGRAMS (IF TEST NOT SET TO Y)***;
   %if %symexist(progs) %then %do;
      %do i=1 %to &progs;
         %put; %put Checking &&prog&i..........; %put;
         data _in;
            infile "&&prog&i" pad missover lrecl=200;
            input @1 record $char200.;
            if index(record,trim(left("&from"))) then do;
               record=tranwrd(record,trim(left("&from")),trim(left("&to")));
               put "WARNING:[PXL] Updating &&prog&i, line " _n_ ":- Changed '&from' to '&to'";
            end;
            else if index(lowcase(record),trim(left(lowcase("&from")))) then
               put "ERROR:[PXL] Text '&from' found in different case (&&prog&i) :- " record;
         run;
         %if &test ne Y %then %do;
            data _null_;
               set _in;
               file "&&prog&i" pad lrecl=200;
               put @1 record $char200.;
            run;
         %end;
         proc datasets lib=work memtype=data nodetails nolist; delete _in; quit;
      %end;
   %end;
   %else %put ERROR:[PXL] No SAS Programs found.;

   *** DELETE TEMP FILES ***;
   proc datasets lib=work memtype=data nodetails nolist; delete _progs; quit;

   *** RESTORE SOURCE AND NOTES OPTIONS ***;
   options &_source &_notes;

%mend searep;
