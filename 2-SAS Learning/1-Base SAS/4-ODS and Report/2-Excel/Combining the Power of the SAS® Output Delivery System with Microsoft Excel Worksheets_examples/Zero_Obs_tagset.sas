

/*******************************************************/
/* Sample tagset that adds a blank worksheet           */
/* resulting from 0 records generated from the         */
/* procedure or DATA step. The $numberOfWorksheets     */ 
/* is check at the bottom of the document to determine */
/* if any worksheets were generated.                   */
/*******************************************************/ 


proc template;
   define tagset tagsets.Excelxp_mod;
     parent=tagsets.excelxp; 
      define event doc;                                                       
         start:                                                               
            eval $numberOfWorksheets 0;                                       
            put "<?xml version=""1.0""";                                      
            putq " encoding=" encoding;                                       
            put "?>" NL NL;                                                   
            putl "<?mso-application progid=""Excel.Sheet""?>";                
            putl "<!-- Generated by the SAS Excelxp Tagset " $tagset_version  
                  " -->";                                                     
            putl "<Workbook xmlns=""urn:schemas-microsoft-com:office:spreadsheet""";                                                                       
            putl "          xmlns:x=""urn:schemas-microsoft-com:office:excel""";                                                                           
            putl "          xmlns:ss=""urn:schemas-microsoft-com:office:spreadsheet""";                                                                    
            putl "          xmlns:html=""http://www.w3.org/TR/REC-html40"">"; 
            putl "<DocumentProperties xmlns=""urn:schemas-microsoft-com:office"">";                                                                        
                                                                              
            do /if operator;                                                  
               putl "<Author>" operator "</Author>";                          
               putl "<LastAuthor>" operator "</LastAuthor>";                  
            done;                                                             
                                                                              
            putl "<Created>" date "T" time "</Created>";                      
            putl "<LastSaved>" date "T" time "</LastSaved>";                  
            putl "<Company>SAS Institute Inc. http://www.sas.com</Company>";  
            putl "<Version>" saslongversion "</Version>";                     
            putl "</DocumentProperties>";                                     
                                                                              
            trigger do_blank_worksheet /if $blank_sheet_name;                 
                                                                              
         finish: 
		    putlog $numberOfWorksheets;
            put "<Worksheet ss:Name=""Missing_data"" />" NL / if cmp($numberOfWorksheets,"0"); 
            putl "</Workbook>";                                               
      end;
   end;
  run;

  /****************************************************/
  /*     Sample data to create o observation data set */
  /****************************************************/

data one;
  set sashelp.class;
  stop;
run;

ods tagsets.ExcelXP_mod file="c:\temp\temp.xls";

proc print data=one;
run;

ods tagsets.ExcelXP_mod close;

